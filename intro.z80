; 1Kraftwerk

; Amstrad CPC 1K intro by PulkoMandy/Shinra for Forever 2020
; Music by Kraftwerk

; ---------------------------------
; Useful system vectors
; ---------------------------------

SYS_PEN  equ $BBDE
	; A = new pen
	; AF is corrupt, and all other registers are preserved

SYS_GETPEN equ $BBE1

SYS_INK equ $BC32
	; A = PEN
	; BC = INK (x2 because flashing)
	; AF, BC, DE and HL are corrupt, and all others are preserved
SYS_SET_BORDER equ $BC38

SYS_MOVE   equ $BBC0
	; AF, BC, DE and HL are corrupt
SYS_SETORG equ $BBC9
	; AF, BC, DE and HL are corrupt
SYS_LINE   equ $BBF6
	; AF, BC, DE and HL are corrupt
	; DE = X
	; HL = Y

SYS_FILL equ $BD52
	; A = pen number
	; HL = work buffer
	; DE = work buffer size

; It starts!
_start

; We expect to be called with RUN", which sets mode 1 and clear the screen.

; Set the color palette
	XOR A
	LD B,$06 ; RED
	LD C,B
	CALL SYS_INK

	LD A,1
	LD B,$00 ; BLACK
	LD C,B
	CALL SYS_INK

	LD A,2
	LD B,$1A ; WHITE
	LD C,B
	CALL SYS_INK

	LD B,$00 ; BLACK
	LD C,B
	CALL SYS_SET_BORDER

	LD A,1
	LD IX, MEN - 4

newpoly
	; Move on to pen 2 if we were asked to
	BIT 6,D ; Shall we chage inks?
	CALL NZ,SYS_PEN

	LD BC, 4
	ADD IX,BC

	LD E,(IX+0)
	LD D,(IX+1)
	LD L,(IX+2)
	LD H,(IX+3)

	CALL SYS_MOVE

newline
	;CALL &BD19
	CALL SYS_LINE

	LD BC, 4
	ADD IX,BC

	LD E,(IX+0)
	LD D,(IX+1)
	LD L,(IX+2)
	LD H,(IX+3)

	BIT 7,D
	JR Z, newline

	; Fill the polygon, we need to move to the fill coordinates
	PUSH DE
	LD A,0x1F
	AND D
	LD D,A
	CALL SYS_MOVE

	; Use the correct color
	CALL SYS_GETPEN
	; fill the poly
	;LD HL,fillbuf but hopefully whatever value is ok
	;LD DE,... DE should holdthe buffer size, but whatever value it has is ok
	CALL SYS_FILL

	POP DE
	LD A,2

	BIT 5,D ; Are we done yet?
	JR Z,newpoly

	LD A,1
	LD HL,env
	CALL &BCBC

	LD A,3
	LD (env+3),A
	LD HL,env
	CALL &BCBC

	; let the music play!
let_the_music
	LD HL,(mp1)

	CALL &BCAA
	JR NC,queue_full

	; Note played, move on to next one for next time
	LD HL,(mp1)
	LD DE,9
	ADD HL,DE

	XOR A
	CP (HL)
	JR NZ,cont

	LD HL,music
cont
	LD (mp1),HL

queue_full

	LD HL,(mp2)

	CALL &BCAA
	JR NC,let_the_music

	; Note played, move on to next one for next time
	LD HL,(mp2)
	LD DE,9
	ADD HL,DE

	XOR A
	CP (HL)
	JR NZ,cont2

	LD HL,ch2.loop
cont2
	LD (mp2),HL
	
	JR let_the_music

; The DATA

; A marker to easily spot the escape sequences (fill, end of stream, set pen)
k equ 0x8000
p equ 0x4000
e equ 0x2000

; The men

MEN
        defw 41, 0, 53, 9, 66, 97, 58, 104, 64, 130, 73, 135, 80, 136
		defw 76, 147, 79, 156, 86, 159, 93, 155, 94, 146, 91, 136, 103, 131
		defw 112, 107, 104, 83, 98, 83, 99, 73, 97, 40, 91, 10, 93, 0
		defw 80, 0, 82, 70, 68, 42, 58, 0, k|55, 4

        defw 229, 0, 233, 6, 231, 39, 233, 86, 222, 102, 227, 129, 239, 137
		defw 236, 151, 245, 158, 251, 155, 252, 139, 266, 134, 280, 111
		defw 278, 90, 264, 116, 253, 9, 256, 4, 250, 1, 246, 12, 247, 72
		defw 243, 29, 241, 0, k|235, 4

        defw 379, 0, 381, 11, 379, 36, 381, 44, 379, 49, 382, 56, 381, 85
		defw 385, 93, 382, 106, 376, 94, 366, 92, 369, 98, 379, 123, 392, 129
		defw 389, 143, 390, 146, 396, 149, 405, 146, 405, 129, 417, 124
		defw 421, 120, 421, 116, 432, 102, 438, 98, 422, 98, 423, 100
		defw 415, 109, 411, 100, 413, 67, 409, 50, 408, 31, 403, 8, 409, 2
		defw 394, 3, 396, 11, 401, 46, 398, 69, 392, 52, 394, 35, 390, 10
		defw 393, 0, k|385, 6

        defw 559, 0, 569, 12, 565, 33, 570, 48, 572, 68, 576, 83, 577, 98
		defw 574, 116, 570, 109, 571, 96, 571, 87, 563, 86, 564, 96, 559, 110
		defw 570, 137, 587, 145, 586, 154, 590, 164, 596, 167, 602, 163
		defw 603, 152, 598, 143, 612, 135, 619, 106, 612, 90, 606, 86
		defw 610, 82, 605, 51, 607, 38, 599, 9, 604, 1, 596, 0, 586, 6
		defw 595, 36, 592, 71, 578, 42, 578, 12, 574, 0, k|p|565, 4

        defw 52,  0,  56, 85,   6, 85,   6, 98, 105, 98, 105, 85,  62, 85,  59, 0, k|55, 4
        defw 236, 0, 240, 85, 190, 85, 190, 98, 289, 98, 289, 85, 246, 85, 243, 0, k|240, 4
        defw 397, 0, 401, 85, 352, 85, 352, 98, 451, 98, 451, 85, 408, 85, 404, 0, k|400, 4
        defw 582, 0, 586, 85, 537, 85, 537, 98, 636, 98, 636, 85, 593, 85, 590, 0, k|e|585, 4


	macro note
	defb \1,\2,\3
	defw \4
	defb \5,\6
	defw \7
	endm

C3 equ 478
D3 equ 426
F3 equ 358
A4 equ 142
C5 equ 119
D5 equ 106
E5 equ 95
F5 equ 89
G5 equ 80
A5 equ 71
C6 equ 60

mp1 defw music
mp2 defw ch2

music
		; Note definition:
		; Byte 0 = channel status
		; Byte 1 = volume enveloppe
		; Byte 2 = tone enveloppe
		; Bytes 3, 4 = tone period
		; Byte 5 = noise period
		; Byte 6 = start volume
		; Byte 7,8 = duration or repeat count
		note 1,1,0,D3,0,0,-2
		note 1,1,0,C3,0,0,-2
		note 1,1,0,D3,0,0,-2
		note 1,1,0,C3,0,0,-2
		note 1,1,0,D3,0,0,-2
		note 1,0,0,C3,0,0,32
		note 1,1,0,F3,0,0,-2
		note 1,0,0,C3,0,0,32
		note 1,1,0,D3,0,0,-2
		note 1,1,0,C3,0,0,-2
		note 1,1,0,D3,0,0,-2
		note 1,1,0,C3,0,0,-2
		note 1,1,0,D3,0,0,-6
		note 1,0,0,C3,0,0,32

		defb 0

ch2
		note 2,0,0,C3,0,0,32*32
ch2.loop
		note 2,3,0,D5,0,0,32*3
		note 2,3,0,D5,0,0,32
		note 2,3,0,F5,0,0,32
		note 2,3,0,D5,0,0,32*3
		note 2,3,0,F5,0,0,32*2
		note 2,3,0,G5,0,0,32
		note 2,3,0,C6,0,0,32
		note 2,3,0,A5,0,0,32*4

		note 2,3,0,A4,0,0,32*3
		note 2,3,0,A4,0,0,32
		note 2,3,0,C5,0,0,32
		note 2,3,0,A4,0,0,32*3
		note 2,3,0,C5,0,0,32*2
		note 2,3,0,D5,0,0,32
		note 2,3,0,G5,0,0,32
		note 2,3,0,E5,0,0,32*4
		defb 0

env
		; Volume enveloppe
		; Byte 0 = number of sections
			; Byte 0 = step count + bit7
			; Byte 1 = step size
			; Byte 2 = pause time
		defb 1, 16,-1,1

ent
		; Tone enveloppe
		; Byte 0 = number of sections
			; Byte 0 = step count
			; Byte 1 = step size
			; Byte 2 = step time

fillbuf
	; Arbitrary free space used by the FILL routine
